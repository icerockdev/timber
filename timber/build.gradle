apply plugin: 'com.android.library'
apply plugin: 'kotlin-multiplatform'

android {
    compileSdkVersion versions.compileSdk

    defaultConfig {
        minSdkVersion versions.minSdk
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
        lintConfig rootProject.file('lint.xml')

        // We run a full lint analysis as build part in CI, so skip vital checks for assemble task.
        checkReleaseBuilds false
    }

    // TODO replace with https://issuetracker.google.com/issues/72050365 once released.
    libraryVariants.all {
        it.generateBuildConfig.enabled = false
    }
}

dependencies {
    implementation deps.kotlin.stdlib.jdk
}

kotlin {
    sourceSets {
        commonMain {
            dependencies {
                implementation deps.kotlin.stdlib.common
            }
        }
        commonTest {
            dependencies {
                implementation deps.kotlin.test.common
                implementation deps.kotlin.test.annotations
            }
        }
        allJvm {
            dependsOn commonMain
            dependencies {
                implementation deps.kotlin.stdlib.jdk
            }
        }
        allJvmTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
            }
        }

        jvm6Main.dependsOn allJvm
        jvm8Main.dependsOn allJvm
        jvm6Test.dependsOn allJvmTest
        jvm8Test.dependsOn allJvmTest
        androidLibMain.dependsOn allJvm
        androidLibTest.dependsOn allJvmTest

        nativeMain {
        }

        nativeTest {
        }
    }

    targets {
        fromPreset(presets.jvm, 'jvm6') {
            compilations.main.source(sourceSets.allJvm)
        }
        fromPreset(presets.jvm, 'jvm8') {
            compilations.main.source(sourceSets.allJvm)
        }
//        fromPreset(presets.js, 'js')

        fromPreset(presets.iosX64, 'iosX64')
        fromPreset(presets.iosArm64, 'iosArm64')

        fromPreset(presets.android, 'androidLib')
    }

    configure([targets.iosX64, targets.iosArm64]) {
        compilations.main.source(sourceSets.nativeMain)
        compilations.test.source(sourceSets.nativeTest)
    }
}

apply from: '../gradle/configpublish.gradle'